#compdef gitter

# Copyright (C) Indrajit Roy <eendroroy@gmail.com>
#
# SPDX-License-Identifier: AGPL-3.0-or-later
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.

_gitter() {
  emulate -L zsh
  setopt extendedglob nullglob

  local -a commands pattern_prefixes
  commands=(
    "git:Run a git command (default)"
    "g:Run a git command (default)"
    "exec:Run an arbitrary command"
    "x:Run an arbitrary command"
    "list:List repositories only"
    "ls:List repositories only"
    "ll:Equivalent to list --verbose command"
    "help:Show this help menu"
    "--filter:Match repo directory name exactly"
    "-f:Match repo directory name exactly"
    "--exclude:Exclude matched repositories instead of including"
    "-e:Exclude matched repositories instead of including"
    "--verbose:Enable verbose mode (only for list command)"
    "-v:Enable verbose mode (only for list command)"
    "--no-color:Disable colored output"
    "--:End of command options, all following arguments are treated as positional arguments"
  )

  pattern_prefixes=(
    "path\:_path_:Match by full path - replace '_path_' with desired value"
    "path\:_path_+:Match by path prefix - replace '_path_' with desired value"
    "path\:+_path_:Match by path suffix - replace '_path_' with desired value"
    "path\:+_path_+:Match by path substring - replace '_path_' with desired value"
    "repo\:_repo_:Match by repository name - replace '_repo_' with desired value"
    "repo\:_repo_+:Match by repository name prefix - replace '_repo_' with desired value"
    "repo\:+_repo_:Match by repository name suffix - replace '_repo_' with desired value"
    "repo\:+_repo_+:Match by repository name substring - replace '_repo_' with desired value"
    "branch\:_branch_:Match by current branch name - replace '_branch_' with desired value"
    "branch\:_branch_+:Match by current branch name prefix - replace '_branch_' with desired value"
    "branch\:+_branch_:Match by current branch name suffix - replace '_branch_' with desired value"
    "branch\:+_branch_+:Match by current branch name substring - replace '_branch_' with desired value"
  )

  _arguments -C \
    '1:command:->command' \
    '--filter:pattern:->pattern' \
    '-f:pattern:->pattern' \
    '*::args:->args'

  case $state in
    command)
      _describe 'gitter commands' commands
      return
      ;;
    pattern)
      _describe 'pattern prefix' pattern_prefixes
      return
      ;;
  esac
}

compdef _gitter gitter
